#version 460 core

// Per vertex values
in float oVertexSquaredDistance;
in vec2 oEuclideanPos;

in float oShieldDirectionDot;
in vec2 oSpikeEuclideanPos;
in vec2 oFeederEuclideanPos;

// Per instance values
// @TODO: Find if there's a way to pass these to the fragment shader actually per instance
in vec4 oVertexColor;
in vec2 oSkinPattern;
in float oLife;

out vec4 FragColor;
out float gl_FragDepth;

void main()
{
    
    // Display shading
    float shadingColMul = pow(1.0 - oVertexSquaredDistance, 0.25);

    // Display skin pattern
    float skinPatternColMul =
        mod(oVertexSquaredDistance, oSkinPattern.x) > oSkinPattern.y
        ? 1.1
        : 1.0;

    // Display forward direction
    float forwardDirColMul = 
        oEuclideanPos.x > -0.025 &&
        oEuclideanPos.x < 0.025 &&
        oEuclideanPos.y > 0
        ? 1.0 + oVertexSquaredDistance * 0.35
        : 1.0;
   
    // Display shield
    float shieldSpan = 0.25;
    float shieldColMul =
        oShieldDirectionDot / oVertexSquaredDistance > (1.0 - shieldSpan) &&
        oVertexSquaredDistance > 0.75
        ? 1.25
        : 1.0;


    // Display spike
    float spikeYDist = 1.0 - oSpikeEuclideanPos.y; 
    float spikeXMul = oSpikeEuclideanPos.x * 2.0; // Make the base of the triangle narrower
    float spikeColMul =
        spikeYDist < 0.25 &&
        abs(spikeXMul) < spikeYDist
        ? 1.25
        : 1.0;


    // Display feeder
    float feederYDist = 1.0 - oFeederEuclideanPos.y; 
    float feederXMul = oFeederEuclideanPos.x * 2.0; // Make the base of the triangle narrower
    float feederColMul =
        feederYDist < 0.25 &&
        abs(feederXMul) < 0.25 - feederYDist
        ? 1.25
        : 1.0;

    
    // Set final color
    FragColor =
        oVertexColor *
        shadingColMul *
        skinPatternColMul *
        forwardDirColMul *
        shieldColMul *
        spikeColMul *
        feederColMul;
    
    
    // Opaqueness reflects life
    FragColor.a = max(pow(oVertexSquaredDistance, (1.0 - oLife) * 5.0), oLife + 0.1);
    FragColor.a *= 0.975; // Still make everything ever so slightly transparent

    // May not be needed
    gl_FragDepth = oVertexSquaredDistance;
}