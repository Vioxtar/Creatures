#version 460 core

layout (std430, binding = 0) buffer CreaturePositions
{
	vec2 Positions[];
};

layout (std430, binding = 1) buffer CreatureVelocities
{
	vec2 Velocities[];
};

layout (std430, binding = 2) buffer CreatureGeneralPurposeVec2
{
	vec2 PhysicsFixes[];
};

layout (std430, binding = 3) buffer CreatureAngles
{
	float Angles[];
};

layout (std430, binding = 4) buffer CreatureAngleVelocities
{
	float AngleVelocities[];
};

layout (std430, binding = 5) buffer CreatureForwardDirections
{
	vec2 ForwardDirections[];
};

layout (std430, binding = 6) buffer CreatureRightDirections
{
	vec2 RightDirections[];
};

layout (std430, binding = 7) buffer CreatureSpikeLocalAngles
{
	float SpikeLocalAngles[];
};

layout (std430, binding = 8) buffer CreatureSpikes
{
	vec4 Spikes[];
};

layout (std430, binding = 9) buffer CreatureFeederLocalAngles
{
	float FeederLocalAngles[];
};

layout (std430, binding = 10) buffer CreatureFeeders
{
	vec4 Feeders[];
};

layout (std430, binding = 11) buffer CreatureShieldLocalAngles
{
	float ShieldLocalAngles[];
};

layout (std430, binding = 12) buffer CreatureShields
{
	vec4 Shields[];
};

layout (std430, binding = 13) buffer CreatureEyeMuscles
{
	vec2 EyeMuscles[];
};

layout (std430, binding = 14) buffer CreatureEyePositions
{
	vec2 EyePositions[];
};

layout (local_size_x = @LOCAL_SIZE@, local_size_y = 1, local_size_z = 1) in;

uniform uint uCreatureCount;
uniform float uVelocityDownscale;
uniform float uAngleVelocityDownscale;
uniform float uCreatureEyeMaxProbeDistance;

#define M_PI 3.1415926535897932384626433832795

void main()
{
	uint gid = gl_GlobalInvocationID.x;
	if (gid >= uCreatureCount)
	{
		return;
	}
	
	// Position and velocity

	// Acquire (and zerofiy) the physics fix calculated in the creature collision program
	vec2 physFix = PhysicsFixes[gid];
	PhysicsFixes[gid] = vec2(0, 0);

	vec2 pos = Positions[gid];
	vec2 vel = Velocities[gid];

	// First update velocity, only then set the position! Otherwise we get fucky collisions
	vel = vel * uVelocityDownscale + physFix;
	pos = pos + vel + physFix;
	
	
	Positions[gid] = pos;
	Velocities[gid] = vel;


	// Angle and angle velocity

	float creatureAngle = Angles[gid];
	float creatureAngleVelocity = AngleVelocities[gid];

	creatureAngleVelocity *= uAngleVelocityDownscale;
	creatureAngle += creatureAngleVelocity;

	Angles[gid] = mod(creatureAngle, M_PI * 2);
	AngleVelocities[gid] = creatureAngleVelocity;



	// Set directions

	vec2 forwardDir = vec2(sin(creatureAngle), cos(creatureAngle));
	ForwardDirections[gid] = forwardDir;

	vec2 rightDir = vec2(forwardDir.y, -forwardDir.x);
	RightDirections[gid] = rightDir;

	float spikeGlobalAngle = creatureAngle + SpikeLocalAngles[gid];
	vec4 spike = Spikes[gid];
	Spikes[gid] = vec4(sin(spikeGlobalAngle), cos(spikeGlobalAngle), spike.zw);

	float feederGlobalAngle = creatureAngle + FeederLocalAngles[gid];
	vec4 feeder = Feeders[gid];
	Feeders[gid] = vec4(sin(feederGlobalAngle), cos(feederGlobalAngle), feeder.zw);

	float shieldGlobalAngle = creatureAngle + ShieldLocalAngles[gid];
	vec4 shield = Shields[gid];
	Shields[gid] = vec4(sin(shieldGlobalAngle), cos(shieldGlobalAngle), shield.zw);


	// Eye positions

	vec2 eyeMuscles = EyeMuscles[gid];
	vec2 eyePosOffset = (forwardDir * eyeMuscles.y + rightDir * eyeMuscles.x) * uCreatureEyeMaxProbeDistance;
	vec2 eyePos = pos + eyePosOffset;
	EyePositions[gid] = eyePos;
	
}