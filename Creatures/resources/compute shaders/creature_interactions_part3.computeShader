#version 460 core

layout (std430, binding = 0) buffer CreatureCollidersCounts
{
	uint CollidersCounts[];
};

layout (std430, binding = 1) buffer CreatureCollidersGivenEnergy
{
	float CollidersGivenEnergy[];
};

layout (std430, binding = 2) buffer CreatureCollidersToPosDirs
{
	vec2 CollidersToPosDirs[];
};

layout (std430, binding = 3) buffer CreatureEnergies
{
	float Energies[];
};

layout (std430, binding = 4) buffer CreatureForwardDirections
{
	vec2 ForwardDirections[];
};

layout (std430, binding = 5) buffer CreatureLives
{
	float Lives[];
};

layout (local_size_x = @LOCAL_SIZE@, local_size_y = 1, local_size_z = 1) in;


uniform uint uCreatureCount;
uniform uint uMaxNumOfColliders;

void main()
{

	uint gid = gl_GlobalInvocationID.x;
	if (gid >= uCreatureCount)
	{
		return;
	}

	if (Lives[gid] <= 0.0)
	{
		return;
	}

	uint collidersStartIndex = gid * uMaxNumOfColliders;

	// Iterate our given energies and add it to our actual energy
	float givenEnergy = 0.0;
	uint collidersCount = min(CollidersCounts[gid], uMaxNumOfColliders);
	for (uint i = 0; i < collidersCount; ++i)
	{
		float energyGivenByCollider = CollidersGivenEnergy[collidersStartIndex + i];
		givenEnergy += energyGivenByCollider;

		// Zerofiy the given energy
		CollidersGivenEnergy[collidersStartIndex + i] = 0.0;
	}
	
	// Submit
	Energies[gid] = givenEnergy;

}