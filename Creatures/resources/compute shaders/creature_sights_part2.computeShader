#version 460 core

layout (std430, binding = 0) buffer CreatureEyePupilSights
{
	float EyePupilSights[];
};

layout (std430, binding = 1) buffer CreatureGeneralPurposeUint
{
	uint EyePupilTargetCreatureIndices[];
};

layout (std430, binding = 2) buffer CreatureGeneralPurposeFloat
{
	float EyePupilActivations[];
};

layout (std430, binding = 3) buffer CreatureGeneralPurposeSecondVec2
{
	vec2 EyePupilC2C1PosDiffNorms[];
};

layout (std430, binding = 4) buffer CreatureShields
{
	vec4 Shields[];
};

layout (std430, binding = 5) buffer CreatureSpikes
{
	vec4 Spikes[];
};

layout (std430, binding = 6) buffer CreatureFeeders
{
	vec4 Feeders[];
};

layout (std430, binding = 7) buffer CreatureForwardDirections
{
	vec2 ForwardDirections[];
};



layout (local_size_x = @LOCAL_SIZE@, local_size_y = 1, local_size_z = 1) in;


uniform uint uCreatureCount;
uniform uint uCreatureEyeNumOfPupilValues;

void main()
{

	uint gid = gl_GlobalInvocationID.x;
	if (gid >= uCreatureCount)
	{
		return;
	}

	// Set default values to-be-written
	float pupilActivation = 0.0;
	float pupilShieldProminence = 0.0;
	float pupilSpikeProminence = 0.0;
	float pupilFeederProminence = 0.0;
	float pupilForwardAimProminence = 0.0;


	// Obtain our pupil creature target
	uint bGID = EyePupilTargetCreatureIndices[gid];
	
	// If bGID == gid it means the pupil didn't see anything
	if (bGID != gid)
	{
		// Get some data about our seen creature
		vec2 c2c1PosDiffNorm = EyePupilC2C1PosDiffNorms[gid];
		vec4 bSpike = Spikes[bGID];
		vec4 bFeeder = Feeders[bGID];
		vec4 bShield = Shields[bGID];
		
		vec2 bSpikeDirection = bSpike.xy;
		float bSpikeState = bSpike.z;
		vec2 bFeederDirection = bFeeder.xy;
		float bFeederState = bFeeder.z;
		vec2 bShieldDirection = bShield.xy;
		float bShieldState = bShield.z;
		float bShieldSpan = bShield.w;

		vec2 bForwardDirection = ForwardDirections[bGID];

		// Set pupil sight values
		pupilActivation = EyePupilActivations[gid];
		pupilShieldProminence = 0.5 * bShieldState + 0.5 * (max(dot(c2c1PosDiffNorm, bShieldDirection), 0) > bShieldSpan ? 1.0 : 0.0);;
		pupilSpikeProminence = 0.5 * bSpikeState + 0.5 * max(dot(c2c1PosDiffNorm, bSpikeDirection), 0);
		pupilFeederProminence = 0.5 * bFeederState + 0.5 * max(dot(c2c1PosDiffNorm, bFeederDirection), 0);
		pupilForwardAimProminence = max(dot(c2c1PosDiffNorm, bForwardDirection), 0);
	}


	// Write data

	uint pupilSightStartIndex = gid * uCreatureEyeNumOfPupilValues;

	EyePupilSights[pupilSightStartIndex + 0] = pupilActivation;
	EyePupilSights[pupilSightStartIndex + 1] = pupilShieldProminence;
	EyePupilSights[pupilSightStartIndex + 2] = pupilSpikeProminence; 
	EyePupilSights[pupilSightStartIndex + 3] = pupilFeederProminence;
	EyePupilSights[pupilSightStartIndex + 4] = pupilForwardAimProminence;

}