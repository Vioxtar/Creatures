#version 460 core

layout (std430, binding = 0) buffer CreaturePositions
{
	vec2 Positions[];
};

layout (std430, binding = 1) buffer CreatureRadii
{
	float Radii[];
};

layout (std430, binding = 2) buffer UniformGrid
{
	uint UGrid[];
};

layout (std430, binding = 3) buffer CreatureAngles
{
	float Angles[];
};

layout (std430, binding = 4) buffer CreatureEyePositions
{
	vec2 EyePositions[];
};

layout (std430, binding = 5) buffer CreatureEyeConeRadii
{
	float EyeConeRadii[];
};

layout (std430, binding = 6) buffer CreatureEyeConeSights
{
	float EyeConeSights[];
};

layout (std430, binding = 7) buffer CreatureGeneralPurposeUint
{
	uint EyePupilTargetCreatureIndices[];
};

layout (std430, binding = 8) buffer CreatureGeneralPurposeFloat
{
	float EyePupilActivations[];
};

layout (std430, binding = 9) buffer CreatureGeneralPurposeSecondVec2
{
	vec2 EyePupilC2C1PosDiffNorms[];
};

layout (std430, binding = 10) buffer CreatureLives
{
	float Lives[];
};

layout (local_size_x = @LOCAL_SIZE@, local_size_y = 1, local_size_z = 1) in;


uniform uint uCreatureCount;
uniform vec2 uSimDimensions;
uniform uvec2 uGridDimensions;
uniform float uGridTileSize;
uniform uint uIndicesInTile;

uniform uint uCreatureEyeNumOfCones;
uniform uint uCreatureEyeNumOfValuesInSingleCone;
uniform uint uCreatureEyeNumOfConesValues;

#define M_PI_HALF	1.5707963267948966192313216916397
#define M_PI		3.1415926535897932384626433832795
#define M_PI_TWO	6.2831853071795864769252867665590


float ApproxAtan(float z)
{
	return (0.97239411 + -0.19194795 * z * z) * z;
}

float ApproxAtan2(float y, float x)
{
	if (x != 0.0)
	{
		if (abs(x) > abs(y)) {
			float z = y / x;
			if (x > 0.0)
			{
				return ApproxAtan(z);
			}
			if (y >= 0.0)
			{
				return ApproxAtan(z) + M_PI;
			}
			return ApproxAtan(z) - M_PI;
		}
		else
		{
			float z = x / y;
			if (y > 0.0) {
				return -ApproxAtan(z) + M_PI_HALF;
			}
			return -ApproxAtan(z) - M_PI_HALF;
		}
	}
	else
	{
		if (y > 0.0)
		{
			return M_PI_HALF;
		}
		else if (y < 0)
		{
			return -M_PI_HALF;
		}
		return 0;
	}
}


void main()
{

	uint gid = gl_GlobalInvocationID.x;
	if (gid >= uCreatureCount)
	{
		return;
	}

	if (Lives[gid] <= 0.0)
	{
		return;
	}




	// Cache our values to be used during iterations
	float eyeConeRadius = EyeConeRadii[gid];
	vec2 aPos = Positions[gid];
	float aAngle = Angles[gid];


	// Used to figure out which creature the pupil will see given several overlapping creatures
	float pupilClosestCreatureDistanceSeen = 0.0;
	bool pupilLandedOnCreature = false;

	// Pupil data package corresponding to closest seen creature, passed to sights program part 2 and 3
	uint pupilTargetCreatureIndex = gid; // If the seen creature index is our own, it means we didn't see a creature!
	float pupilActivation = 0.0;
	vec2 pupilC2C1PosDiffNorms = vec2(0, 0);

	// Cone values to be spatially indexed, initialize default values to 0.0
	float coneSights[@CREATURE_EYE_NUM_OF_CONES_VALUES@] = float[@CREATURE_EYE_NUM_OF_CONES_VALUES@](0.0);





	// Begin optimized UGrid iteration: we start from the center and move outwards, bailing out if we've gathered enough data
	float simWidth = uSimDimensions.x;
	float simHeight = uSimDimensions.y;
	
	uint gridXDim = uGridDimensions.x;
	uint gridYDim = uGridDimensions.y;

	uint tileCreatureCapacity = uIndicesInTile - 1;


	// Find the tile our eye position is mapped to
	// It's fine if it's out of grid bounds, x/y Start/End values will prevent out of range memory accesses
	
	vec2 pupilPos = EyePositions[gid];

	float remappedXPupilPos = pupilPos.x + (simWidth / 2.0);
	float remappedYPupilPos = pupilPos.y + (simHeight / 2.0);

	int xTileIndex = int(floor(remappedXPupilPos * gridXDim / simWidth));
	int yTileIndex = int(floor(remappedYPupilPos * gridYDim / simHeight));
	
	ivec2 eyePupilTileXYIndices = ivec2(xTileIndex, yTileIndex);


	// Find how many tiles our eye cone radius can cover
	uint tilesCoveredByEyeConeRadius = uint(ceil(eyeConeRadius / uGridTileSize)) + 1;

	// Iteration directions used to move outwards from the center point
	ivec2 uniformGridIterationDirections[4] = ivec2[4](ivec2(0, 1), ivec2(1, 0), ivec2(0, -1), ivec2(-1, 0));

	uint conesActivated = 0;

	for (int diagDownLeft = 0; diagDownLeft < tilesCoveredByEyeConeRadius; ++diagDownLeft)
	{
		if (diagDownLeft > 1 && conesActivated >= uCreatureEyeNumOfCones)
		{
			break;
		}

		bool onCenterTile = diagDownLeft == 0;
		ivec2 activeTilePos = eyePupilTileXYIndices + ivec2(-1, -1) * diagDownLeft;
		uint faceSteps = onCenterTile ? 1 : diagDownLeft * 2;
		for (uint face = 0; face <= (onCenterTile ? 0 : 3); ++face)
		{
			
			ivec2 iterationDir = onCenterTile ? ivec2(0, 0) : uniformGridIterationDirections[face];
			
			for (uint step = 1; step <= faceSteps; ++step)
			{
				
				// Are we inside simulation bounds?
				bool insideBounds = 0 <= activeTilePos.x && activeTilePos.x < gridXDim && 0 <= activeTilePos.y && activeTilePos.y < gridYDim;
				if (!insideBounds)
				{
					activeTilePos += iterationDir;
					continue;
				}

				// Find the 1D tile again
				uint tile = activeTilePos.x + gridXDim * activeTilePos.y;

				// Iterate all other creatures currently in the tile
				uint tileCountIndex = tile * uIndicesInTile;
				uint creaturesInTile = min(UGrid[tileCountIndex], tileCreatureCapacity);
				uint lastPopulatedIndex = tileCountIndex + creaturesInTile;

				for (uint b = tileCountIndex + 1; b <= lastPopulatedIndex; b++)
				{
					uint bGID = UGrid[b];
					if (bGID >= uCreatureCount)
					{
						continue;
					}

					// We can't see ourselves
					if (bGID == gid)
					{
						continue;
					}
				
					// Gather some positioning data about our potentially viewed creature
					vec2 bPos = Positions[bGID];
					float bRad = Radii[bGID];

					vec2 bPosToPupilPosDiff = pupilPos - bPos;
					float bPosToPupilPosDist = length(bPosToPupilPosDiff);
					float bPosToPupilPosDistMinusBRad = bPosToPupilPosDist - bRad;

					// Did the creature land in our eye cone radius?
					if (bPosToPupilPosDistMinusBRad <= eyeConeRadius)
					{
						// The creature is within our eye...


						// Is it closer than previously seen creatures?
						if (bPosToPupilPosDist < pupilClosestCreatureDistanceSeen || !pupilLandedOnCreature)
						{

							pupilClosestCreatureDistanceSeen = bPosToPupilPosDist;
							pupilLandedOnCreature = true;

							// Remember the closest creature index, activation and C2C1 normalized pos diff
							pupilTargetCreatureIndex = bGID;
							pupilActivation = max(0.0, min(1.0 - bPosToPupilPosDistMinusBRad / eyeConeRadius, 1.0));
							
							vec2 c2c1PosDiff = aPos - bPos;
							float c2c1PosDist = length(c2c1PosDiff);
							pupilC2C1PosDiffNorms = c2c1PosDiff / c2c1PosDist;
						}
	
						
						// Find out the relative angle between our eye and the observed creature
						vec2 bPosToPupilPosDirection = bPosToPupilPosDiff / bPosToPupilPosDist;
						
						float absAng = ApproxAtan2(bPosToPupilPosDirection.x, -bPosToPupilPosDirection.y);
						float relAng = mod(absAng + aAngle, M_PI_TWO);

						// Relative angle is between 0 and 2pi, map this to our cone indices
						uint coneID = uint(floor(float(uCreatureEyeNumOfCones) * relAng / M_PI_TWO));
						uint coneIndex = coneID * uCreatureEyeNumOfValuesInSingleCone;

						// Load our data into the mapped cone
						float activation = max(0.0, min(1.0 - bPosToPupilPosDistMinusBRad / eyeConeRadius, 1.0));
						float oldConeActivation = coneSights[coneIndex];
						coneSights[coneIndex] = max(activation, oldConeActivation);

						if (oldConeActivation == 0.0)
						{
							conesActivated++;
						}
						
					}
				}

				activeTilePos += iterationDir;
			}
		}
	}

	// Send pupil data
	EyePupilTargetCreatureIndices[gid] = pupilTargetCreatureIndex;
	EyePupilActivations[gid] = pupilActivation;
	EyePupilC2C1PosDiffNorms[gid] = pupilC2C1PosDiffNorms;
	
	// Set cone sightings
	uint conesSightStartIndex = gid * uCreatureEyeNumOfConesValues;
	for (uint i = 0; i < uCreatureEyeNumOfConesValues; ++i)
	{
		EyeConeSights[conesSightStartIndex + i] = coneSights[i];
	}
}