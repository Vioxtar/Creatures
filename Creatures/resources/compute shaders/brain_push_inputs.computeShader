#version 460 core

layout (std430, binding = 0) buffer CreatureBrainsStructures
{
	uint BrainsStructures[];
};

layout (std430, binding = 1) buffer CreatureBrainsNodes
{
	float BrainsNodes[];
};

layout (std430, binding = 2) buffer CreatureLives
{
	float Lives[];
};

layout (std430, binding = 3) buffer CreatureEnergies
{
	float Energies[];
};

layout (std430, binding = 4) buffer CreatureMeats
{
	float Meats[];
};

layout (std430, binding = 5) buffer CreatureForwardDirections
{
	vec2 ForwardDirections[];
};

layout (std430, binding = 6) buffer CreatureRightDirections
{
	vec2 RightDirections[];
};

layout (std430, binding = 7) buffer CreatureVelocities
{
	vec2 Velocities[];
};

layout (std430, binding = 8) buffer CreatureAngleVelocities
{
	float AngleVelocities[];
};

layout (std430, binding = 9) buffer CreatureEyePupilSights
{
	float EyePupilSights[];
};

layout (std430, binding = 10) buffer CreatureEyeConeSights
{
	float EyeConeSights[];
};

layout (std430, binding = 11) buffer CreatureSkinPatterns
{
	vec2 SkinPatterns[];
};

layout (local_size_x = @LOCAL_SIZE@, local_size_y = 1, local_size_z = 1) in;

uniform uint uCreatureCount;

uniform uint uMaxNumOfStructureIndices;
uniform uint uMaxNumOfNodesInBrain;

uniform float uCreatureMaxEnergy;
uniform float uCreatureMaxLife;
uniform float uCreatureMaxMeat;

uniform float uCreatureMaxVelocityLength;
uniform float uCreatureMaxAngleVelocityLength;

uniform uint uCreatureEyeNumOfPupilValues;
uniform uint uCreatureEyeNumOfConesValues;

uniform uint uNumOfTemporals;

void main()
{
	uint gid = gl_GlobalInvocationID.x;
	if (gid >= uCreatureCount)
	{
		return;
	}
	
	float life = Lives[gid];

	if (life <= 0.0)
	{
		return;
	}

	uint brainNodesStartIndex = gid * uMaxNumOfNodesInBrain;
	uint brainStructureStartIndex = gid * uMaxNumOfStructureIndices;

	uint numOfLevels = BrainsStructures[brainStructureStartIndex];
	uint numOfInputs = BrainsStructures[brainStructureStartIndex + 1];
	
	// Acquire the input nodes' values range in our brain data, we always assume there's enough room to push our inputs!
	uint firstInputNodeIndex = brainNodesStartIndex;
	uint firstNonInputNodeIndex = brainNodesStartIndex + numOfInputs;
	
	// Push values

	// Body parameters
	BrainsNodes[firstInputNodeIndex + 0] = min(Energies[gid] / uCreatureMaxEnergy, 1.0); // Clamped because excess energy (to go to meat)
	BrainsNodes[firstInputNodeIndex + 1] = life / uCreatureMaxLife;
	BrainsNodes[firstInputNodeIndex + 2] = Meats[gid] / uCreatureMaxMeat;

	// Movements
	vec2 velocity = Velocities[gid];
	vec2 forwardDir = ForwardDirections[gid];
	vec2 rightDir = RightDirections[gid];

	float normForthBack = dot(velocity, forwardDir) / uCreatureMaxVelocityLength;
	float normRightLeft = dot(velocity, rightDir) / uCreatureMaxVelocityLength;
	
	BrainsNodes[firstInputNodeIndex + 3] = max(-1.0, min(normForthBack, 1.0)); // Clamped because we may be travelling more than max velocity
	BrainsNodes[firstInputNodeIndex + 4] = max(-1.0, min(normRightLeft, 1.0)); // Clamped because we may be travelling more than max velocity

	// Angle movements
	float angleVelocityNorm = AngleVelocities[gid] / uCreatureMaxAngleVelocityLength;
	BrainsNodes[firstInputNodeIndex + 5] = max(-1.0, min(angleVelocityNorm, 1.0)); // Clamped because we may be travelling more than max velocity;

	// Eye pupil sights (all are normalized)
	uint pupilSightStartIndex = gid * uCreatureEyeNumOfPupilValues;

	BrainsNodes[firstInputNodeIndex + 6] = EyePupilSights[pupilSightStartIndex + 0]; // Activation
	BrainsNodes[firstInputNodeIndex + 7] = EyePupilSights[pupilSightStartIndex + 1]; // Shield prominence
	BrainsNodes[firstInputNodeIndex + 8] = EyePupilSights[pupilSightStartIndex + 2]; // Spike prominence
	BrainsNodes[firstInputNodeIndex + 9] = EyePupilSights[pupilSightStartIndex + 3]; // Feeder prominence
	BrainsNodes[firstInputNodeIndex + 10] = EyePupilSights[pupilSightStartIndex + 4]; // Forward aim prominence
	BrainsNodes[firstInputNodeIndex + 11] = EyePupilSights[pupilSightStartIndex + 5]; // Skin hue difference
	BrainsNodes[firstInputNodeIndex + 12] = EyePupilSights[pupilSightStartIndex + 6]; // Skin value
	BrainsNodes[firstInputNodeIndex + 13] = EyePupilSights[pupilSightStartIndex + 7]; // Skin saturation
	BrainsNodes[firstInputNodeIndex + 14] = EyePupilSights[pupilSightStartIndex + 8]; // Skin pattern difference
	BrainsNodes[firstInputNodeIndex + 15] = EyePupilSights[pupilSightStartIndex + 9]; // Life
	BrainsNodes[firstInputNodeIndex + 16] = EyePupilSights[pupilSightStartIndex + 10]; // Energy
	BrainsNodes[firstInputNodeIndex + 17] = EyePupilSights[pupilSightStartIndex + 11]; // Radius

	// Eye cone sights
	uint inputOffset = 18;
	uint conesSightStartIndex = gid * uCreatureEyeNumOfConesValues;
	for (uint i = 0; i < uCreatureEyeNumOfConesValues; ++i)
	{
		BrainsNodes[firstInputNodeIndex + inputOffset] = EyeConeSights[conesSightStartIndex + i];
		inputOffset++;
	}

	// Temporals
	uint firstTemporalOutputIndex = (gid + 1) * uMaxNumOfNodesInBrain - uNumOfTemporals;
	for (uint i = 0; i < uNumOfTemporals; ++i)
	{
		BrainsNodes[firstInputNodeIndex + inputOffset] = BrainsNodes[firstTemporalOutputIndex + i];
		inputOffset++;
	}
}